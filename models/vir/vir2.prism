// virus example
// gxn/dxp 29/06/2007
// network is grid of size N

mdp

// probabilities
const double infect=0.5; // probability virus infects a node
const double detect1=0.5; // probability virus detected by firewall of high/low node
const double detect2=0.3; // probability virus detected by firewall of barrier node

// low nodes (those above the ceil(N/2) row)

const double detect11=detect1; 
const double detect12=detect1; 

// barrier nodes (those in the ceil(N/2) row)

const double detect21=detect2;
const double detect22=detect2;

// high nodes (those below the ceil(N/2) row)


// first column (1..N)
module n11

	s11 : [0..2] init 2; // node uninfected
	// 0 - node uninfected
	// 1 - node uninfected but firewall breached
	// 2 - node infected 

	// firewall attacked (from an infected neighbour)
	[attack11_21] (s11=0) ->  detect11 : true + (1-detect11) : (s11'=1);
	[attack11_12] (s11=0) ->  detect11 : true + (1-detect11) : (s11'=1);
	// if the firewall has been breached tries to infect the node	
	[] (s11=1) -> infect : (s11'=2) + (1-infect) : (s11'=0);
	// if the node is infected, then it tries to attack its neighbouring nodes
	[attack21_11] (s11=2) -> true;
	[attack12_11] (s11=2) -> true;
	[clean_11] (s11=2) -> (s11'=0);
	[] (s11=2) -> (s11'=2);
endmodule

module n21

	s21 : [0..2] init 2; // node uninfected
	// 0 - node uninfected
	// 1 - node uninfected but firewall breached
	// 2 - node infected 

	// firewall attacked (from an infected neighbour)
	[attack21_22] (s21=0) -> detect21 : true + (1-detect21) : (s21'=1);
	[attack21_11] (s21=0) -> detect21 : true + (1-detect21) : (s21'=1);
	// if the firewall has been breached tries to infect the node	
	[] (s21=1) -> infect : (s21'=2) + (1-infect) : (s21'=0);
	// if the node is infected, then it tries to attack its neighbouring nodes
	[attack22_21] (s21=2) -> true;
	[attack11_21] (s21=2) -> true;
	[clean_21] (s21=2) -> (s21'=0);
	[] (s21=2) -> (s21'=2);
endmodule

// second column
module n12=n21[s21=s12,detect21=detect12,attack22_21=attack22_12,attack11_21=attack11_12,attack21_22=attack12_22,attack21_11=attack12_11,clean_21=clean_12] endmodule

module n22
	
	s22 : [0..2] init 2; // node uninfected
	// 0 - node uninfected
	// 1 - node uninfected but firewall breached
	// 2 - node infected 
	
	// firewall attacked (from an infected neighbour)
	[attack22_12] (s22=0) -> detect22 : true + (1-detect22) : (s22'=1);
	[attack22_21] (s22=0) -> detect22 : true + (1-detect22) : (s22'=1);
	// if the firewall has been breached tries to infect the node	
	[] (s22=1) -> infect : (s22'=2) + (1-infect) : (s22'=0);
	// if the node is infected, then it tries to attack its neighbouring nodes
	[attack12_22] (s22=2) -> true;
	[attack21_22] (s22=2) -> true;
	[clean_22] (s22=2) -> (s22'=0);
	[] (s22=2) -> (s22'=2);
	
endmodule

// reward structure (number of attacks)
rewards "attacks"

	// corner nodes

	[attack11_12] true : 1;
	[attack11_21] true : 1;

	// edge nodes

	[attack12_11] true : 1;
	[attack12_22] true : 1;
	[attack21_11] true : 1;
	[attack21_22] true : 1;

	// middle nodes
	 
	[attack22_12] true : 1;
	[attack22_21] true : 1;  

endrewards

rewards "clean"

	[clean_11] true : 1;
	[clean_12] true : 1;
	[clean_21] true : 1;
	[clean_22] true : 1;

endrewards

rewards "withvirus"

	 s11=2 : 1;
	 s12=2 : 1;
	 s21=2 : 1;
	 s22=2 : 1;

endrewards
